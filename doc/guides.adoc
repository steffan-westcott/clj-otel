= Guides
:toc:
:toclevels: 3
:icons: font
ifdef::env-github[]
:tip-caption: :bulb:
:note-caption: :information_source:
:important-caption: :heavy_exclamation_mark:
:caution-caption: :fire:
:warning-caption: :warning:
endif::[]

== Add telemetry to your library or an application

To add telemetry to a library or application, use xref:concepts.adoc#_automatic_instrumentation[automatic instrumentation] and/or xref:concepts.adoc#_manual_instrumentation[manual instrumentation] as described in the following subsections.

=== Add automatic instrumentation when running an application

* Follow the guide to <<_run_with_the_opentelemetry_auto_instrumentation_agent,run the application with the OpenTelemetry instrumentation agent>> to add automatic instrumentation.

[#_add_manual_instrumentation_to_your_library_or_application_code]
=== Add manual instrumentation to your library or application code

* Add project dependency :
+
.`deps.edn`
[source,clojure]
----
{;; ...
 :deps {com.github.steffan-westcott/clj-otel-api {:mvn/version "0.1.1"}}}
----

* Follow the guides in the remainder of this section to make changes to your library or application code to add manual instrumentation.

==== Create a synchronous span using the current context

* Use https://cljdoc.org/d/com.github.steffan-westcott/clj-otel-api/CURRENT/api/steffan-westcott.clj-otel.api.trace.span#with-span![`steffan-westcott.clj-otel.api.trace.span/with-span!`] to wrap a body of forms in a xref:concepts.adoc#_traces[span], where the xref:concepts.adoc#_current_context[current context] is used to keep track of (potentially nested) spans:
+
[source,clojure]
----
(defn get-nums [args]
  (span/with-span! {:name "Getting numbers"}
    (fetch-nums args)))
----

==== Create a synchronous span using explicit context

* Use https://cljdoc.org/d/com.github.steffan-westcott/clj-otel-api/CURRENT/api/steffan-westcott.clj-otel.api.trace.span#with-span-binding[`steffan-westcott.clj-otel.api.trace.span/with-span-binding`] to wrap a body of forms in a span, where the context to use is passed in as the `:parent` option.
The new context containing the new span is bound to `context*` in this example:
+
[source,clojure]
----
(defn get-nums [context args]
  (span/with-span-binding [context* {:parent context
                                     :name "Getting numbers"}]
    (fetch-nums context* args)))
----

[#_create_an_asynchronous_span]
==== Create an asynchronous span

* Use https://cljdoc.org/d/com.github.steffan-westcott/clj-otel-api/CURRENT/api/steffan-westcott.clj-otel.api.trace.span#async-span[`steffan-westcott.clj-otel.api.trace.span/async-span`] to start a new span that ends when either success/failure callback `respond`/`raise` is evaluated:
+
[source,clojure]
----
(defn get-nums-async [context args respond raise]
  (span/async-span {:parent context
                    :name   "Getting numbers"
                    :kind   :client}
                   (fn [context* respond* raise*]
                     (fetch-nums-async context* args respond* raise*))
                   respond
                   raise))
----

==== Add attributes to a span

* Use the `:attributes` option to add xref:concepts.adoc#_attributes[attributes] when creating a span:
+
[source,clojure]
----
(defn user-info [user-id]
  (span/with-span! {:name "Getting user info"
                    :attributes {:system/user-id user-id}}
    (fetch-user-info user-id)))
----

* Alternatively, use https://cljdoc.org/d/com.github.steffan-westcott/clj-otel-api/CURRENT/api/steffan-westcott.clj-otel.api.trace.span#add-span-data![`steffan-westcott.clj-otel.api.trace.span/add-span-data!`] including the `:attributes` option to add attributes to an existing span.
+
By default, the span in the current context is updated:
+
[source,clojure]
----
(defn user-info [user-id]
  (span/add-span-data! {:attributes {:system/user-id user-id}})
  (fetch-user-info user-id))
----
+
Use the `:context` option to specify the context containing the span to update:
+
[source,clojure]
----
(defn user-info [context user-id]
  (span/add-span-data! {:context context
                        :attributes {:system/user-id user-id}})
  (fetch-user-info context user-id))
----

==== Add an event to a span

* Use https://cljdoc.org/d/com.github.steffan-westcott/clj-otel-api/CURRENT/api/steffan-westcott.clj-otel.api.trace.span#add-span-data![`steffan-westcott.clj-otel.api.trace.span/add-span-data!`] including the `:event` option to add an xref:concepts.adoc#_events[event] to an existing span.
The event may include xref:concepts.adoc#_attributes[attributes].
+
By default, the event is added to the span in the current context:
+
[source,clojure]
----
(defn complete-stage [job]
  (span/add-span-data! {:event {:name "Job stage completed"
                                :attributes {:service.workflow.job/stage (:stage job)}}})
  (notify-watchers job))
----
+
Use the `:context` option to specify the context containing the span to add the event to:
+
[source,clojure]
----
(defn complete-stage [context job]
  (span/add-span-data! {:context context
                        :event {:name "Job stage completed"
                                :attributes {:service.workflow.job/stage (:stage job)}}})
  (notify-watchers context job))
----

==== Add an exception event to a span

NOTE: Events for exceptions thrown in spans which leave (escape) the span's scope are *automatically added* to the span.
This applies to synchronous and asynchronous spans.
For https://cljdoc.org/d/com.github.steffan-westcott/clj-otel-api/CURRENT/api/steffan-westcott.clj-otel.api.trace.span#async-span[`async-span`], exceptions are "thrown" using the `raise` callback function.

* Use https://cljdoc.org/d/com.github.steffan-westcott/clj-otel-api/CURRENT/api/steffan-westcott.clj-otel.api.trace.span#add-exception![`steffan-westcott.clj-otel.api.trace.span/add-exception!`] to add an event describing an exception to an existing span.
Use this function to capture details about caught (non-escaping) exceptions.
+
The exception event may include xref:concepts.adoc#_attributes[attributes], controlled by the `:attributes` option.
+
By default, the exception event is added to the span in the current context:
+
[source,clojure]
----
(defn process-args [args]
  (try
    (parse-args args)
    (catch Throwable e
      (span/add-exception! e {:escaping? false
                              :attributes {:app/args args}})
      {:result :parse-error})))
----
+
Use the `:context` option to specify the context containing the span to add the exception event to:
+
[source,clojure]
----
(defn process-args [context args]
  (try
    (parse-args args)
    (catch Throwable e
      (span/add-exception! e {:context context
                              :escaping? false
                              :attributes {:app/args args}})
      {:result :parse-error})))
----

=== Work with HTTP client and server spans

The guides in this section describe xref:concepts.adoc#_semantic_conventions[semantic conventions] support for HTTP xref:concepts.adoc#_span_kind[client and server spans].

[#_use_ring_middleware_for_server_span_support]
==== Use Ring middleware for server span support

* Use Ring middleware https://cljdoc.org/d/com.github.steffan-westcott/clj-otel-api/CURRENT/api/steffan-westcott.clj-otel.api.trace.http#wrap-server-span[`steffan-westcott.clj-otel.api.trace.http/wrap-server-span`] to add HTTP server span support to a Ring handler.
+
The enabled support features vary with the selected middleware options.
The middleware can be configured to work in applications that run with or without the OpenTelemetry instrumentation agent.
It also supports both synchronous (1-arity) and asynchronous (3-arity) handlers.
+
An example using Jetty with server spans created by the agent:
+
[source,clojure]
----
(defn handler [request]
  ;; ...
  )

(def service
  (-> handler
      (trace-http/wrap-server-span {:create-span? false
                                    :server-name  "my-server"})))

(defonce server
  (jetty/run-jetty #'service {:port 8080 :join? false}))
----

[#_use_pedestal_interceptors_for_server_span_support]
==== Use Pedestal interceptors for server span support

* Use interceptors https://cljdoc.org/d/com.github.steffan-westcott/clj-otel-api/CURRENT/api/steffan-westcott.clj-otel.api.trace.http#server-span-interceptors[`steffan-westcott.clj-otel.api.trace.http/server-span-interceptors`] to add HTTP server span support to a Pedestal HTTP service.
+
The enabled support features vary with the selected interceptor options.
The interceptors can be configured to work in applications that run with or without the OpenTelemetry instrumentation agent.
+
An example using Jetty with server spans created by the agent:
+
[source,clojure]
----
(def routes
  (route/expand-routes
    [[["/" (trace-http/server-span-interceptors {:create-span? false
                                                 :server-name  "my-server"})
       ;; ...
       ]]]))

(def service-map
  {::http/routes routes
   ::http/type   :jetty
   ::http/port   8080
   ::http/join?  false})

(defonce server
  (http/start (http/create-server service-map)))
----

==== Add route data to a server span

* Use https://cljdoc.org/d/com.github.steffan-westcott/clj-otel-api/CURRENT/api/steffan-westcott.clj-otel.api.trace.http#add-route-data![`steffan-westcott.clj-otel.api.trace.http/add-route-data!`] to add the matched route to a server span.
+
By default, the route data is added to the span in the current context:
+
[source,clojure]
----
(trace-http/add-route-data! "/rooms/:room-id")
----
+
Use the `:context` option to specify the context containing the span to add the route data to:
+
[source,clojure]
----
(trace-http/add-route-data! "/rooms/:room-id" {:context context})
----

==== Manually add HTTP response data to a client span

NOTE: When an application is run with the OpenTelemetry instrumentation agent, HTTP response data is *automatically added* to HTTP client spans for supported clients.

* Use https://cljdoc.org/d/com.github.steffan-westcott/clj-otel-api/CURRENT/api/steffan-westcott.clj-otel.api.trace.http#add-client-span-response-data![`steffan-westcott.clj-otel.api.trace.http/add-client-span-response-data!`] to add HTTP response data to a client span.
Use this function when working with an HTTP client not supported by the OpenTelemetry instrumentation agent.
+
By default, the HTTP response data is added to the span in the current context:
+
[source,clojure]
----
(trace-http/add-client-span-response-data! response)
----
+
Use the `:context` option to specify the context containing the span to add the HTTP response data to:
+
[source,clojure]
----
(trace-http/add-client-span-response-data! response {:context context})
----

==== Manually propagate context in an HTTP client request

NOTE: When an application is run with the OpenTelemetry instrumentation agent, context is *automatically propagated* in HTTP client requests for supported clients.

* Use https://cljdoc.org/d/com.github.steffan-westcott/clj-otel-api/CURRENT/api/steffan-westcott.clj-otel.context#\->headers[`steffan-westcott.clj-otel.context/\->headers`] to get headers to merge (inject) with other headers in the HTTP request to be issued, for the purpose of xref:concepts.adoc#_context_propagation[context propagation].
Use this function when working with an HTTP client not supported by the OpenTelemetry instrumentation agent.
+
By default, the current context is propagated:
+
[source,clojure]
----
(let [context-headers (context/->headers)
      request' (update request :headers merge context-headers)]
  ;; ...
  )
----
+
Use the `:context` option to specify the context to be propagated:
+
[source,clojure]
----
(let [context-headers (context/->headers {:context context})
      request' (update request :headers merge context-headers)]
  ;; ...
  )
----

== Configure and run an application with telemetry

The options below determine what telemetry data is exported from an application as it runs.
Select one of these options and follow the linked guide:

* <<_run_with_the_opentelemetry_auto_instrumentation_agent,Run with the OpenTelemetry auto-instrumentation agent>>
+
Select this option to export telemetry data as a result of automatic and manual instrumentation.
* <<_run_with_autoconfigured_sdk,Run with autoconfigured SDK>>
+
Select this option to export telemetry data from manual instrumentation only; The SDK will be configured using properties or environment variables.
* <<_run_with_programmatically_configured_sdk,Run with programmatically configured SDK>>
+
Select this option to export telemetry data from manual instrumentation only; The SDK will be configured programmatically in the application.
* <<_run_without_agent_or_sdk,Run without agent or SDK>>
+
Select this option to mute all telemetry data.

TIP: Traces telemetry data is muted in the last option, or by setting the properties `otel.traces.exporter` and `otel.metrics.exporter` to `none` (the default is `otlp`) when using either of the first two options (autoconfiguration).

[#_run_with_the_opentelemetry_auto_instrumentation_agent]
=== Run with the OpenTelemetry auto-instrumentation agent

* Download the latest version of the OpenTelemetry instrumentation agent JAR, the file `opentelemetry-javaagent.jar` from the https://github.com/open-telemetry/opentelemetry-java-instrumentation/releases[releases page].
The agent JAR includes the SDK and all its dependencies.
* Configure the agent and SDK, using properties and environment variables.
See the https://opentelemetry.io/docs/instrumentation/java/automatic/agent-config/[agent and SDK configuration documentation].
* When running the application, enable the agent with the `-javaagent` JVM flag.

For an example application `my-app`, with `deps.edn` to export traces only using OTLP over gRPC, use an alias like the following:

.`deps.edn`
[source,clojure]
[.small]
----
{;; ...
 :aliases {
   :otel {:jvm-opts ["-javaagent:path/to/opentelemetry-javaagent.jar"
                     "-Dotel.resource.attributes=service.name=my-app"
                     "-Dotel.traces.exporter=otlp"
                     "-Dotel.metrics.exporter=none"
                     "-Dotel.exporter.otlp.traces.protocol=grpc"]}}}
----

[#_run_with_autoconfigured_sdk]
=== Run with autoconfigured SDK

* Add project dependencies:
** _Required:_ `io.opentelemetry/opentelemetry-sdk-extension-autoconfigure` for the SDK itself and SDK autoconfiguration.
** _Required:_ `io.opentelemetry/opentelemetry-exporter-???` for any xref:concepts.adoc#_exporters[exporters] referenced in the configuration.
See xref:reference.adoc#_java_exporter_libraries[Java exporter libraries supported by autoconfiguration].
** _Optional:_ `io.opentelemetry/opentelemetry-sdk-extension-resources` for various xref:concepts.adoc#_resources[resources] to be automatically added to telemetry data.
** _Optional:_ `io.opentelemetry/opentelemetry-sdk-extension-aws` for various resources describing the AWS execution environment to be automatically added to telemetry data.
** _Optional:_ `io.opentelemetry/opentelemetry-extension-aws` for text map propagator implementing the AWS X-Ray Trace Header propagation protocol.
** _Optional:_ `io.opentelemetry/opentelemetry-extension-trace-propagators` for text map propagators implementing OpenTracing Basic Tracers, Jaeger and B3 propagation protocols.
** _Optional:_ `io.grpc/grpc-netty-shaded`, `io.grpc/grpc-protobuf` and `io.grpc/grpc-stub` to use Netty for gRPC transport rather than the default OkHttp (see example below).
This is not needed if gRPC is not used by any exporters or the application.

* Configure the SDK using properties and environment variables.
See https://github.com/open-telemetry/opentelemetry-java/tree/main/sdk-extensions/autoconfigure[SDK autoconfigure configuration documentation].

For an example application `my-app`, with `deps.edn` to export traces only using OTLP over gRPC with Netty transport, use an alias like the following:

.`deps.edn`
[source,clojure]
[.small]
----
{;; ...
 :aliases {
   :otel {:jvm-opts ["-Dotel.resource.attributes=service.name=my-app"
                     "-Dotel.traces.exporter=otlp"
                     "-Dotel.metrics.exporter=none"
                     "-Dotel.exporter.otlp.traces.protocol=grpc"]
          :extra-deps {io.opentelemetry/opentelemetry-sdk-extension-autoconfigure {:mvn/version "1.13.0-alpha"}
                       io.opentelemetry/opentelemetry-sdk-extension-resources     {:mvn/version "1.13.0"}
                       io.opentelemetry/opentelemetry-exporter-otlp-trace         {:mvn/version "1.13.0"}
                       io.grpc/grpc-netty-shaded                                  {:mvn/version "1.45.1"}
                       io.grpc/grpc-protobuf                                      {:mvn/version "1.45.1"}
                       io.grpc/grpc-stub                                          {:mvn/version "1.45.1"}}}}}
----

[#_run_with_programmatically_configured_sdk]
=== Run with programmatically configured SDK

* Add project dependencies:
** _Required:_ `com.github.steffan-westcott/clj-otel-sdk` for the SDK itself and a Clojure wrapper of SDK configuration
** _Required:_ `com.github.steffan-westcott/clj-otel-exporter-???` for Clojure wrapped versions of any xref:concepts.adoc#_exporters[exporters] referenced in the configuration.
See xref:reference.adoc#_clojure_exporter_modules[Clojure wrapped versions of exporters supported by autoconfiguration].
** _Optional:_ `com.github.steffan-westcott/clj-otel-sdk-extension-resources` for Clojure wrapped versions of various xref:concepts.adoc#_resources[resources] to add to telemetry data.
** _Optional:_ `com.github.steffan-westcott/clj-otel-sdk-extension-aws` for Clojure wrapped versions of resources describing the AWS execution environment.
** _Optional:_ `com.github.steffan-westcott/clj-otel-extension-aws` for Clojure wrapped text map propagator implementing the AWS X-Ray Trace Header propagation protocol.
** _Optional:_ `com.github.steffan-westcott/clj-otel-extension-trace-propagators` for Clojure wrapped text map propagators implementing OpenTracing Basic Tracers, Jaeger and B3 propagation protocols.
** _Optional:_ `io.grpc/grpc-netty-shaded`, `io.grpc/grpc-protobuf` and `io.grpc/grpc-stub` to use Netty for gRPC transport rather than the default OkHttp (see example below).
This is not needed if gRPC is not used by any exporters or the application.
* At application start, use https://cljdoc.org/d/com.github.steffan-westcott/clj-otel-sdk/CURRENT/api/steffan-westcott.clj-otel.sdk.otel-sdk#init-otel-sdk![`steffan-westcott.clj-otel.sdk.otel-sdk/init-otel-sdk!`] to configure and set an OpenTelemetry SDK instance as the global OpenTelemetry instance.
* At application end, use https://cljdoc.org/d/com.github.steffan-westcott/clj-otel-sdk/CURRENT/api/steffan-westcott.clj-otel.sdk.otel-sdk#close-otel-sdk![`steffan-westcott.clj-otel.sdk.otel-sdk/close-otel-sdk!`] to close down activities of the SDK instance.

For an example application `my-app`, with `deps.edn` to export traces only using OTLP over gRPC with Netty transport, use deps like the following:

.`deps.edn`
[source,clojure]
[.small]
----
{;; ...
 :deps {com.github.steffan-westcott/clj-otel-sdk                      {:mvn/version "0.1.1"}
        com.github.steffan-westcott/clj-otel-sdk-extension-resources  {:mvn/version "0.1.1"}
        com.github.steffan-westcott/clj-otel-exporter-otlp-grpc-trace {:mvn/version "0.1.1"}
        io.grpc/grpc-netty-shaded                                     {:mvn/version "1.45.1"}
        io.grpc/grpc-protobuf                                         {:mvn/version "1.45.1"}
        io.grpc/grpc-stub                                             {:mvn/version "1.45.1"}}}
----

To configure the SDK at start and close down at end, the application could have functions like the following:

.`example/app.clj`
[source,clojure]
----
(ns example.app
  (:require [steffan-westcott.clj-otel.exporter.otlp-grpc-trace :as otlp-grpc-trace]
            [steffan-westcott.clj-otel.resource.resources :as res]
            [steffan-westcott.clj-otel.sdk.otel-sdk :as sdk]))

(defn init-otel! []
  (sdk/init-otel-sdk!
    "my-app"
    {:resources [(res/host-resource)
                 (res/os-resource)
                 (res/process-resource)
                 (res/process-runtime-resource)]
     :tracer-provider
       {:span-processors
         [{:exporters [(otlp-grpc-trace/span-exporter)]}]}}))

(defn close-otel! []
  (sdk/close-otel-sdk!))
----

[#_run_without_agent_or_sdk]
=== Run without agent or SDK

There are no steps to add dependencies or otherwise configure the application in order to run it without the agent or SDK.

NOTE: An application run without the OpenTelemetry instrumentation agent or SDK will not export any telemetry data.
Usage of the OpenTelemetry API (manual instrumentation) in the application will invoke no-op implementations.

== Use the OpenTelemetry Collector

The xref:concepts.adoc#_opentelemetry_collector[OpenTelemetry Collector] is used to manage telemetry data, as an alternative to applications exporting data directly to telemetry backends.

=== Deploy the OpenTelemetry Collector

See the https://opentelemetry.io/docs/collector/getting-started/[getting started documentation].

=== Configure the OpenTelemetry Collector

See the https://opentelemetry.io/docs/collector/configuration/[configuration documentation].

This example Collector configuration has a traces pipeline where:

* Trace data are received by the Collector as OTLP over gRPC
* Memory usage in the Collector process is limited
* Traces are batched in the Collector prior to export to the backend
* Traces are exported by the Collector to the Jaeger backend on host `jaeger`

.`otel-collector-config.yaml`
[source,yaml]
----
receivers:
  otlp:
    protocols:
      grpc:

processors:
  memory_limiter:
    check_interval: 1s
    limit_mib: 750
  batch:

exporters:
  jaeger:
    endpoint: jaeger:14250
    insecure: true

service:
  pipelines:
    traces:
      receivers: [ otlp ]
      processors: [ memory_limiter, batch ]
      exporters: [ jaeger ]
----
